plugins {
    id "com.android.application"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.trans.pvztv"
    ndkVersion = "27.2.12479018"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.trans.pvztv"
        minSdk = 21
        targetSdk = 33
        versionCode = 2
        versionName = "0.0.0"
        versionNameSuffix = '-' + new Date().format("yyMMdd")

        ndk {
            abiFilters "armeabi-v7a"
        }
    }

    signingConfigs {
        // 本文件已被 git 跟踪, 不要在此处修改签名配置, 需要自定义签名配置请参考 README.md
        release {
            keyAlias = keystoreProperties.getProperty("keyAlias", "key0")
            keyPassword = keystoreProperties.getProperty("keyPassword", "wewewe")
            storeFile = file(keystoreProperties.getProperty("storeFile", "D:/key.keystore"))
            storePassword = keystoreProperties.getProperty("storePassword", "wewewe")
        }
    }

    buildTypes {
        debug {
            debuggable = true
            jniDebuggable = true
        }

        release {
            debuggable = false
            jniDebuggable = false
            minifyEnabled = true
            shrinkResources = true
            signingConfig = signingConfigs.release
        }
    }

    flavorDimensions += "version"

    productFlavors {
        create("v111") {
            dimension = "version"
            applicationIdSuffix = ".v111"
            versionName = "1.1.1"
            externalNativeBuild {
                cmake {
                    arguments "-DPVZ_VERSION=111"
                }
            }
        }

        create("v115") {
            dimension = "version"
            applicationIdSuffix = ".v115"
            versionName = "1.1.5"
            externalNativeBuild {
                cmake {
                    arguments "-DPVZ_VERSION=115"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path = "src/main/cpp/CMakeLists.txt"
        }
    }

    lintOptions {
        checkReleaseBuilds = false
        abortOnError = false
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "PvZ-TV-v${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree("dir": "libs", "include": ["*.jar"])
}
